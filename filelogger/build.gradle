apply plugin: 'com.android.library'

/*
define in local.properties these variables:
bintray.user=[BINTRAY_USERNAME]
bintray.apikey=[BINTRAY_API_KEY]
bintray.gpg.password=[DEPENDS_WHETHER_GPG_AUTO_SIGNING_KEY_HAS_A_PASSPHRASE]
developer.id=[USERNAME]
developer.name=[FULL_NAME]
developer.email=[EMAIL]
*/

ext {
    LIB_VERSION = '1.0.1'

    bintrayRepo = 'maven'
    bintrayName = 'android-file-logger'

    publishedGroupId = 'com.github.bosphere.android-filelogger'
    libraryName = 'Android-FileLogger'
    artifact = 'filelogger'

    libraryDescription = 'A general-purpose logging library with built-in support to save logs to file.'

    siteUrl = 'https://github.com/bosphere/Android-FileLogger'
    gitUrl = 'https://github.com/bosphere/Android-FileLogger.git'

    libraryVersion = "$LIB_VERSION"

    Properties properties = new Properties()
    properties.load(project.rootProject.file('local.properties').newDataInputStream())
    developerId = properties.getProperty('developer.id')
    developerName = properties.getProperty('developer.name')
    developerEmail = properties.getProperty('developer.email')

    licenseName = 'The Apache Software License, Version 2.0'
    licenseUrl = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
    allLicenses = ["Apache-2.0"]
}

android {
    compileSdkVersion 26
    buildToolsVersion "26.0.0"

    defaultConfig {
        minSdkVersion 9
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
}

dependencies {
}

///////////////////////////////////////////////////////////////////////////////////////////

apply plugin: 'com.github.dcendents.android-maven'

group = publishedGroupId                               // Maven Group ID for the artifact

install {
    repositories.mavenInstaller {
        // This generates POM.xml with proper parameters
        pom {
            project {
                packaging 'aar'
                groupId publishedGroupId
                artifactId artifact

                // Add your description here
                name libraryName
                description libraryDescription
                url siteUrl

                // Set your license
                licenses {
                    license {
                        name licenseName
                        url licenseUrl
                    }
                }
                developers {
                    developer {
                        id developerId
                        name developerName
                        email developerEmail
                    }
                }
                scm {
                    connection gitUrl
                    developerConnection gitUrl
                    url siteUrl

                }
            }
        }
    }
}

///////////////////////////////////////////////////////////////////////////////////////////

apply plugin: 'com.jfrog.bintray'

version = libraryVersion

task sourcesJar(type: Jar) {
    from android.sourceSets.main.java.srcDirs
    classifier = 'sources'
}

task javadoc(type: Javadoc) {
    source = android.sourceSets.main.java.srcDirs
    classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}
artifacts {
    archives javadocJar
    archives sourcesJar
}

// Bintray
Properties properties = new Properties()
properties.load(project.rootProject.file('local.properties').newDataInputStream())

bintray {
    user = properties.getProperty("bintray.user")
    key = properties.getProperty("bintray.apikey")

    configurations = ['archives']
    pkg {
        repo = bintrayRepo
        name = bintrayName
        desc = libraryDescription
        websiteUrl = siteUrl
        vcsUrl = gitUrl
        licenses = allLicenses
        publish = true
        publicDownloadNumbers = true
        version {
            desc = libraryDescription
            gpg {
                sign = true //Determines whether to GPG sign the files. The default is false
                passphrase = properties.getProperty("bintray.gpg.password")
                //Optional. The passphrase for GPG signing'
            }
        }
    }
}

///////////////////////////////////////////////////////////////////////////////////////////